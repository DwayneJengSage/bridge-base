group = 'org.sagebionetworks'
ext.name = 'bridge-base'
version = '0.1.' + (new Date()).format(
        'yyyyMMddHHmm', TimeZone.getTimeZone("UTC"))

/*** Java ***/

apply plugin: 'java'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

jar {
    group = project.group
    baseName = project.ext.name
    version = project.version
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:' + commonsLang3Ver
    compile 'com.google.guava:guava:' + guavaVer
    compile 'joda-time:joda-time:' + jodaVer
    compile 'commons-codec:commons-codec:' + commonsCodecVer
    compile 'org.apache.shiro:shiro-core:' + shiroVer
    compile 'org.bouncycastle:bcprov-jdk15on:' + bcVer
    compile 'org.bouncycastle:bcpkix-jdk15on:' + bcVer
    compile 'com.amazonaws:aws-java-sdk-dynamodb:' + awsVer
    compile 'redis.clients:jedis:' + redisVer
    testCompile 'junit:junit:' + junitVer
    testCompile 'org.mockito:mockito-core:' + mockitoVer
    testRuntime 'ch.qos.logback:logback-classic:' + logbackVer
    testRuntime 'ch.qos.logback:logback-core:' + logbackVer
}

/*** IDE ***/

apply plugin: 'eclipse'
apply plugin: 'idea'

/*** Code Quality Tools ***/

apply plugin: 'findbugs'
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply plugin: 'jdepend'
tasks.withType(JDepend) {
    reports {
        xml.enabled = false
        text.enabled = true
    }
}

apply plugin: 'pmd'
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply plugin: 'jacoco'
jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
    }
}

/*** Wrapper ***/

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

/*** Maven publishing ***/

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "s3://${System.env.REPO_MAVEN_BUCKET}"
            credentials(AwsCredentials) {
                accessKey "${System.env.REPO_PUBLISHER_KEY}"
                secretKey "${System.env.REPO_PUBLISHER_SECRET}"
            }
        }
    }
}
